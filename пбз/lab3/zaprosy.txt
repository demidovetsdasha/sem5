// 1. Список всех товаров и их категорий
MATCH (t:Товар)
RETURN t.название AS Название, t.категория AS Категория;

// 2. Информация о покупателях
MATCH (p:Покупатель)-[:НАХОДИТСЯ_В]->(l:Пункт_назначения)
RETURN 
  CASE p.тип 
    WHEN "Юридическое лицо" THEN p.название
    WHEN "Физическое лицо" THEN p.имя
  END AS Покупатель,
  p.адрес AS Адрес, 
  p.реквизиты AS Реквизиты,
  l.населенный_пункт AS Населенный_пункт;


// 3. Информация из накладных об отпущенном товаре
MATCH (n:Накладная)-[:СОДЕРЖИТ]->(t:Товар)
RETURN t.название AS Товар, n.цена AS Цена, n.количество AS Количество;

// 4. Товары, отправленные в конкретный город
MATCH (n:Накладная)-[:ДОСТАВЛЯЕТСЯ_В]->(l:Пункт_назначения {населенный_пункт: "Минск"}),
      (n)-[:СОДЕРЖИТ]->(t:Товар)
RETURN t.название AS Товар;

// 5. Накладные для юрлиц
MATCH (n:Накладная)-[:ВЫПИСАНА_ДЛЯ]->(p:Покупатель {тип: "Юридическое лицо"})
RETURN n.номер AS Накладная, p.название AS Покупатель;

// 6. Названия регионов, куда отправлялись промышленные товары
MATCH (t:Товар {категория: "Промышленные"})<-[:СОДЕРЖИТ]-(n:Накладная)-[:ДОСТАВЛЯЕТСЯ_В]->(l:Пункт_назначения)
RETURN DISTINCT l.регион AS Регион;

// 7. Список покупателей и общее количество товаров, которые они приобрели
MATCH (p:Покупатель)<-[:ВЫПИСАНА_ДЛЯ]-(n:Накладная)
RETURN 
  CASE p.тип
    WHEN "Юридическое лицо" THEN p.название
    WHEN "Физическое лицо" THEN p.имя
  END AS Покупатель,
  SUM(n.количество) AS ОбщееКоличество;


// 8. Общий объем товара, отправленного в определенный регион
MATCH (n:Накладная)-[:ДОСТАВЛЯЕТСЯ_В]->(l:Пункт_назначения {регион: "Минская область"}),
      (n)-[:СОДЕРЖИТ]->(t:Товар)
RETURN t.название AS Товар, SUM(n.количество) AS Объем;

// 9. Все покупатели в определенной стране
MATCH (n:Накладная)-[:ВЫПИСАНА_ДЛЯ]->(p:Покупатель),
      (n)-[:ДОСТАВЛЯЕТСЯ_В]->(l:Пункт_назначения {страна: "Беларусь"})
RETURN DISTINCT 
  CASE p.тип
    WHEN "Юридическое лицо" THEN p.название
    WHEN "Физическое лицо" THEN p.имя
  END AS Покупатель;


// 10. Общий доход по каждому типу товара
MATCH (n:Накладная)-[:СОДЕРЖИТ]->(t:Товар)
RETURN t.категория AS Категория, SUM(n.цена * n.количество) AS ОбщийДоход
ORDER BY ОбщийДоход DESC;



